#lang hydromel

# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

component counter(LIMIT : natural)
    constant WIDTH = unsigned_width(LIMIT)
    port clear  : in  bit
    port up     : in  bit
    port down   : in  bit
    port value  : out unsigned(WIDTH)
    port bottom : out bit
    port top    : out bit

    top    = value == LIMIT
    bottom = value == 0

    value = register(0 when clear,
                unsigned(WIDTH) |if up then
                    if top then
                        0
                    else
                        value + 1
                else if down then
                    if bottom then
                        LIMIT
                    else
                        value - 1
                else
                    value|)
end
