#lang hydromel

# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

interface producer(T : type)
    port valid : out bit
    port ready : in  bit
    port data  : out T
end

component fifo1(T : type)
    port c : flip producer(T)
    port p : producer(T)

    signal full : bit = register(0, if full then
                                        c.valid or not p.ready
                                    else
                                        c.valid and not p.ready)

    signal write  = c.valid and (if full then p.ready else not p.ready)
    signal r_data = register(zero(T), c.data when write)

    c.ready = p.ready or not full
    p.valid = c.valid or full
    p.data  = if full then r_data else c.data
end
