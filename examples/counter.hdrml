#lang hydromel

component counter(LIMIT : unsigned(2)) # TODO should accept any size
    constant WIDTH = unsigned_width(LIMIT)
    port clear  : in  bit
    port up     : in  bit
    port down   : in  bit
    port value  : out unsigned(WIDTH)
    port bottom : out bit
    port top    : out bit

    top    = value == LIMIT
    bottom = value == 0

    value = register(0 when clear,
                if up then
                    if top then
                        0
                    else
                        cast(unsigned(WIDTH), value + 1)
                else if down then
                    if bottom then
                        LIMIT
                    else
                        cast(unsigned(WIDTH), value - 1)
                else
                    value)
end
